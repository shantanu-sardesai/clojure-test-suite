(ns clojure.core-test.val
  (:require [clojure.test :refer [are deftest is testing]]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists val
  (deftest test-val
    (testing "`val` on map-entry-like things"
      (is (= :v (val (first {:k :v}))))
      (is (contains? #{:two :v} (val (first {:k :v, :one :two}))))
      ;; Note: the following may be built on shaky ground, per Rich:
      ;; https://groups.google.com/g/clojure/c/FVcrbHJpCW4/m/Fh7NsX_Yb7sJ
      (is (= 'v (val #?(:cljs    (cljs.core/MapEntry. 'k 'v nil)
                        :lpy     (map-entry 'k 'v)
                        :phel    (map-entry 'k 'v)
                        :default (clojure.lang.MapEntry/create 'k 'v)))))
      (is (= :b (val (first (hash-map :a :b)))))
      (when-var-exists sorted-map
        (is (= :b (val (first (sorted-map :a :b))))))
      (when-var-exists array-map
        (is (= :b (val (first (array-map :a :b)))))))
    (testing "`val` throws on lots of things"
      (are [arg] (p/thrown? (val arg))
                 nil
                 0
                 '()
                 '(1 2)
                 {}
                 {1 2}
                 []
                 [1 2]
                 #{}
                 #{1 2}))))
