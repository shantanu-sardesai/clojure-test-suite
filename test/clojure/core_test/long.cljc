(ns clojure.core-test.long
  (:require [clojure.test :as t :refer [are deftest is]]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists long
  (deftest test-long
    ;; There is no platform independent predicate to test specifically
    ;; for a long. In ClojureJVM, it's an instance of `java.lang.Long`,
    ;; but there is no predicate for it. Here, we just test whether it's
    ;; a fixed-length integer of some sort.
    (is (int? (int 0)))
    #?(:cljr (is (instance? System.Int64 (long 0)))
       :clj (is (instance? java.lang.Long (long 0))))

    ;; Check conversions and rounding from other numeric types
    (are [expected x] (= expected (long x))
      -9223372036854775808 -9223372036854775808
      0                    0
      9223372036854775807  9223372036854775807
      1                    1N
      0                    0N
      -1                   -1N
      1                    1.0M
      0                    0.0M
      -1                   -1.0M
      1                    1.1
      -1                   -1.1
      1                    1.9
      1                    1.1M
      -1                   -1.1M
      #?@(:cljs []
          :default
          [1    3/2
           -1   -3/2
           0    1/10
           0    -1/10]))

    #?@(:lpy [] ; Python VMs integer types are arbitrary precision and have no min or max
        :cljs [] ; In CLJS all numbers are double-precision floating point
        :default
        ;; `long` throws outside the range of 9223372036854775807 ... -9223372036854775808
        [(is (p/thrown? (long -9223372036854775809)))
         (is (p/thrown? (long 9223372036854775808)))])

    ;; Check handling of other types
    #?@(:cljr
        [(is (= 0 (long "0")))
         (is (p/thrown? (long :0)))
         (is (p/thrown? (long [0])))
         (is (p/thrown? (long nil)))]
        
        :lpy
        [(is (= 0 (long "0")))
         (is (p/thrown? (long :0)))
         (is (p/thrown? (long [0])))
         (is (p/thrown? (long nil)))]

        :cljs
        [(is (= 0 (long "0"))) ; JavaScript peeking through?
         (is (NaN? (long :0)))
         (is (NaN? (long [0])))
         (is (= 0 (long nil)))] ; Hm. Interesting.
        
        :default
        [(is (p/thrown? (long "0")))
         (is (p/thrown? (long :0)))
         (is (p/thrown? (long [0])))
         (is (p/thrown? (long nil)))])))
