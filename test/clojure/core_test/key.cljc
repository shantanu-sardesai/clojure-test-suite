(ns clojure.core-test.key
  (:require [clojure.test :refer [are deftest is testing]]
            [clojure.core-test.portability #?(:cljs :refer-macros :default :refer) [when-var-exists] :as p]))

(when-var-exists key
  (deftest test-key
    (testing "`key` on map-entry-like things"
      (is (= nil (key (first {nil nil}))))
      (is (= :k (key (first {:k :v}))))
      (is (contains? #{:k :one} (key (first {:k :v, :one :two}))))
      ;; Note: the following may be built on shaky ground, per Rich:
      ;; https://groups.google.com/g/clojure/c/FVcrbHJpCW4/m/Fh7NsX_Yb7sJ
      (is (= 'k (key #?(:cljs    (cljs.core/MapEntry. 'k 'v nil)
                        :lpy     (map-entry 'k 'v)
                        :phel    (map-entry 'k 'v)
                        :default (clojure.lang.MapEntry/create 'k 'v)))))
      (when-var-exists sorted-map
        (is (= :a (key (first (sorted-map :a :b))))))
      (is (= :a (key (first (hash-map :a :b)))))
      (when-var-exists array-map
        (is (= :a (key (first (array-map :a :b)))))))
    (testing "`key` throws on lots of things"
      (are [arg] (p/thrown? (key arg))
                 nil
                 0
                 '()
                 '(1 2)
                 {}
                 {1 2}
                 []
                 [1 2]
                 #{}
                 #{1 2}))))
